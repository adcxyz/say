TITLE:: SayBuf
summary:: prepare text-to-speech in buffers
categories:: Speech Synthesis
related:: Classes/Say, Classes/Buffer

DESCRIPTION::
SayBuf helps the Say class to render text-to-speech as soundfiles and buffers,
in order to use play them as sound sources within SC.

Rendering say-events to soundfiles is on average 15-40x faster than realtime (tested on a 2014 Macbook Pro), so with short text fragments, writing soundfiles and reading them into buffers is fast enough for near-realtime use.

SayBuf has two schemes for preparing buffers:
By default, it uses a ring of 100 buffers that get used round-robin.
This leaves the most recent buffers loaded, and is recommended.

Creating and freeing buffers individually is also supported.

code::
SayBuf.usesRoundRobin
::

code::
// prepare a sayEvent, use bufAction to say it as soon as ready:
SayBuf.prepare( (text: "this is 'say' from a buffer!", voice: \Alex, bufAction: (_.play)) );

// Say the example texts for all voices backwards with buffers:
(
Tdef(\allVoicesByBuf, {
	Say.voices.do { |voicedict|
		// make the say event
		var ev = (
			voice: voicedict.name.postcs,
			text: voicedict.exampleText,
			// this will play the buffer instantly when it is ready:
			bufAction: { |buf|
				(instrument: \saybuf, buf: buf.bufnum,
					pan: 1.0.rand2, // stereo pan
					rate: exprand(-0.5, -2) // randomize playback rate
				).postln.play;
			}
		);
		// render it to a buffer
		SayBuf.prepare(ev, s);
		1.wait;
	};
	// wait long enough for the last buffer to finish playback
	5.wait;
	// if you used the bufs pool, the free all buffers and delete temp soundfiles
	// SayBuf.freeBufs;
}).play;
)

// The newer high-res voices can have rates between 90 and 720 syllables per second.
// say ignores rates outside that range and plays them at default rate!
// Therefore, Say clips them to this range, so that e.g. too-fast rates run at maimum rate.
(
Tdef(\rateTest, {
	var rates = (16..0).linexp(0, 16, 90, 720).round(0.01).postln;
	rates.do { |rate|
		var ev = (
			rate: rate,
			text: "rate test 1 2 3 4 5",
			bufAction: { |buf|
				(instrument: \saybuf,
					buf: buf.bufnum,
					pan: 1.0.rand2, // stereo pan
				).postln.play;
			}
		);
		// render it to a buffer
		SayBuf.prepare(ev, s);
		1.wait;
	};
}).play;
)


::

CLASSMETHODS::

private:: initClass
METHOD:: dir
the dir where SayBuf writes temporary soundfiles

METHOD:: freeBuf
free a buffer and delete its soundfile. By default, this waits for
buffer duration * 1.1 + server latency before freeing the buffer.

METHOD:: clearDir
clear all temp soundfiles from dir

METHOD:: bufs
the dictionary of recently created buffers

METHOD:: bufring
the ring of reserved buffers that get re-used when using roundRobin buffers.

METHOD:: prepare
prepare a buffer for a code::sayEvent::

ARGUMENT:: sayEvent
an event as used by Say, e.g.
code::
x = (type: \say, text: "speak me", voice: "Alex");
x.play;
::

ARGUMENT:: server
the server on which to render the buffer

ARGUMENT:: action
the action to perform when buffer is ready
code::
SayBuf.prepare(x, s, { |buf| buf.postln.play });
::

EXAMPLES::

code::
// prepare four words, say them several times by buffers
(
Tdef(\textbuf, {
	var t0 = Main.elapsedTime;
	var words = [\Multi, \Folder, \Sunny, \Beach];
	var mybufs = ();
	words.do { |txt|
		SayBuf.prepare((lang: \en, text: txt.asString, bufAction: { |b|
			mybufs.put(txt, b);
		}))
	};
	// wait until ready to go:
	while { mybufs.size < 4 } { 0.1.wait };
	"loading 4 bufs took % seconds.\n".postf(Main.elapsedTime - t0);

	// now say them, with rate and pan args
	5.do { |i|
		words.do { |txt|
			var buf = mybufs[txt];
			var rate = 1.1 ** i;
			// use default SayBuf instrument:
			(instrument: \saybuf, buf: buf, pan: 1.0.rand2,
				rate: rate).postln.play;
			(buf.duration / rate / (i+1)).wait;
		};
	};

	// cleanup buffers and files when done:
	1.wait;

	mybufs.do(SayBuf.freeBuf(_));

}).play;
)
::

subsection::More on Preparing Speech to Buffers

// TODO: measure all prepare variants and recommend best ones.
// e.g. try with SayBuf.prepare in fork and non-fork versions.

code::

// use saybuf to write to a buffer and use it:
SayBuf.prepare((type: \say, voice: "Anna", text: "saybuf", bufAction: { |b| b.play }));


// prepare a single file by hand, to measure time it takes:
(
var x = File.mkdir("~/Desktop/saybuf".standardizePath);
var dict = Say.voices.choose;
var starttime = Main.elapsedTime;
var path = "~/Desktop/saybuf/%.aiff".format(dict.name).standardizePath;
var event = (type: \say, voice: dict.name, text: dict.exampleText, wait: true,
	cmds: "-o" + path).play;
var writetime = Main.elapsedTime - starttime;
var sfdur = SoundFile.openRead(path).duration;
"// dur, prepTime, ratio: ".postln;
[sfdur, writetime, sfdur / writetime].round(0.001).postln;
)

// prepare a lot of them in sequence with WAITING and measure overall time:
(
var x = File.mkdir("~/Desktop/saybuf".standardizePath);
var starttime = Main.elapsedTime;
var delta, sfdur;
var pathnames = Say.voices.collect { |dict|
	var path = "~/Desktop/saybuf/%.aiff".format(dict.name).standardizePath;
	"preparing : %\n".postf(dict.name);
	(type: \say, voice: dict.name, text: dict.exampleText,
		cmds: "-o" + path, wait: true).play;
	path
};
var writetime = Main.elapsedTime - starttime;
var durs = pathnames.collect { |path|
	SoundFile.openRead(path).duration;
};
var ratio = durs.sum / writetime;
"// durSum, prepTime, ratio: ".postln;
[durs.sum, writetime, ratio].round(0.001).postln;
)

// on MacBook Pro 2015 2,9 GHz Intel Core i5:
// durSum, prepTime, ratio:
[ 238.263, 16.778, 14.201 ]


// prepare a lot of files by pipeline using maxPIDs
(
Say.pendingPIDs.clear;
Say.maxPIDs = 16;

Tdef(\prepAll, {
	var start = Main.elapsedTime;
	var durSum, prepTime, ratio;

	Say.voices.do { |dict|
		Say.pendingPIDs.size.postln;
		while { Say.pendingPIDs.size >= (Say.maxPIDs - 1) } { 0.01.wait };
		"preparing : %\n".postf(dict.name);
		SayBuf.prepare(
			(voice: dict.name, text: dict.exampleText, bufAction: { |b|
				var writetime = Main.elapsedTime - start;
				var dur = b.duration;
				ratio = dur / writetime;
				d.put(dict.name.asSymbol, [dur, writetime, ratio])
			})
		);
		0.01.wait;
	};

	while { d.size < Say.voices.size } { 0.01.wait };
	durSum = d.sum(_[0]);
	prepTime = Main.elapsedTime - start;
	ratio = durSum / prepTime;
	"// durSum, prepTime, ratio: ".postln;
	[durSum, prepTime, ratio].round(0.001).postln;

}).play;
)
// on MacBook Pro 2015 2,9 GHz Intel Core i5:
// not really faster, even a bit slower ...
// durSum, prepTime, ratio:
[ 238.263, 18.19, 13.099 ]

::
